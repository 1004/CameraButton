/*
 * Copyright (C) 2017 Artem Glugovsky
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import groovy.json.JsonSlurper

apply plugin: 'com.jfrog.bintray'

def parser = new JsonSlurper()
def rootProjectInfo = file("${rootProject.projectDir}/project-info.json")
def projectInfo = file("$projectDir/project-info.json")

def rootProperties = parser.parseText(rootProjectInfo.text)
def properties = parser.parseText(projectInfo.text)

ext {
    artifactName = properties.name.toLowerCase()
    artifactVersion = properties.version ?: rootProperties.version

    projectGroup = rootProperties.group.toLowerCase()
    packageName = "$projectGroup:$artifactName"
    versionDescription = properties.description
    siteUrl = properties.siteUrl ?: rootProperties.siteUrl
    gitUrl = properties.gitUrl ?: rootProperties.gitUrl

    developerId = properties.developer?.id ?: rootProperties.developer.id
    developerName = properties.developer?.name ?: rootProperties.developer.name
    developerEmail = properties.developer?.email ?: rootProperties.developer.email
}

def bintrayUser = hasProperty('bintrayUser') ? bintrayUser : System.getenv('bintray.user')
def bintrayKey = hasProperty('bintrayKey') ? bintrayKey : System.getenv('bintray.key')

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    publish = true
    pkg {
        name = packageName
        repo = 'maven'
        userOrg = developerId
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = artifactVersion
            desc = versionDescription
            released = new Date()
            vcsTag = "v$artifactVersion"
        }
    }
}